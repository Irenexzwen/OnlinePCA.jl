"""
    hvg(;binfile::AbstractString="", rowmeanlist::AbstractString="", rowvarlist::AbstractString="", rowcv2list::AbstractString="", outdir::AbstractString=".")

This function perform highly variable genes, which is an feature selection method in scRNA-seq studies.

Input Arguments
---------
- `binfile` is a Julia Binary file generated by `csv2bin` function.
- `rowmeanlist` : The mean of each row of matrix. The CSV file is generated by `sumr` functions.
- `rowvarlist` : The variance of each row of matrix. The CSV file is generated by `sumr` functions.
- `rowcv2list` : The cv2 of each row of matrix. The CSV file is generated by `sumr` functions.
- `outdir` : The directory specified the directory you want to save the result.

Output Files
---------
- `HVG_useForFit.csv` : Parameters to estimate the highly variable genes.
- `HVG_a0.csv` : Parameters to estimate the highly variable genes.
- `HVG_a1.csv` : Parameters to estimate the highly variable genes.
- `HVG_afits.csv` : Parameters to estimate the highly variable genes.
- `HVG_varFitRatio.csv` : Parameters to estimate the highly variable genes.
- `HVG_df.csv` : Parameters to estimate the highly variable genes.
- `HVG_pval.csv` : Parameters to estimate the highly variable genes.

Reference
---------
- [Highly Variable Genes](http://pklab.med.harvard.edu/scw2014/subpop_tutorial.html)
"""
function hvg(;binfile::AbstractString="", rowmeanlist::AbstractString="", rowvarlist::AbstractString="", rowcv2list::AbstractString="", outdir::AbstractString=".")
    # Initialization
    N, M = nm(binfile)
    rowmeanvec = readcsv(rowmeanlist)
    rowvarvec = readcsv(rowvarlist)
    rowcv2vec = readcsv(rowcv2list)
    useForFit = []
    pm = []
    counter = 0
    pval = ones(N)
    for n = 1:N
        if rowcv2vec[n] > 0.3
            counter = counter + 1
            push!(pm, n)
        end
    end
    thr = percentile(rowmeanvec[pm], 50)
    for n = 1:N
        if rowmeanvec[n] >= thr
            push!(useForFit, n)
        end
    end

    # Fitting
	println("Highly Variable Genes are calculated...")
    data = DataFrame(Y=rowcv2vec[useForFit], X=1 ./ rowmeanvec[useForFit])
    fit = glm(@formula(Y ~ X), data, Gamma(), IdentityLink())
    a0, a1 = coef(fit)
    afit = a1 ./ rowmeanvec .+ a0
    varFitRatio = rowvarvec ./ (afit .* rowmeanvec .* rowmeanvec)
    df = M - 1

    # P-value
    for n = 1:N
        pval[n] = ccdf(Chisq(df), varFitRatio[n]*df)
    end

    # Save
    writecsv(joinpath(outdir, "HVG_useForFit.csv"), useForFit)
    writecsv(joinpath(outdir, "HVG_a0.csv"), a0)
    writecsv(joinpath(outdir, "HVG_a1.csv"), a1)
    writecsv(joinpath(outdir, "HVG_afit.csv"), afit)
    writecsv(joinpath(outdir, "HVG_varFitRatio.csv"), varFitRatio)
    writecsv(joinpath(outdir, "HVG_df.csv"), df)
    writecsv(joinpath(outdir, "HVG_pval.csv"), pval)
end
